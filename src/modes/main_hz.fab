/*
 * MAIN HZ
 * Contains measured hz meter and tap count
 */

vars /main_hz
    U[5] hz_digits = U[5](0, 0, 0, 0, 0)
    U main_hz_color = $10
    U tap_count = 0
    UU start_index = 0
    UU end_index = 0
    Bool prev_poll_on = false

fn main_hz_init_bg()
    ppu_reset_addr($2064)
    {PPUDATA}($F0)
    {PPUDATA}($F0)
    {PPUDATA}('.')
    {PPUDATA}($F0)
    {PPUDATA}($F0)
    {PPUDATA}('*')
    ppu_reset_addr($20A4)
    {PPUDATA}($00)
    {PPUDATA}('·')
    {PPUDATA}('T')
    {PPUDATA}('A')
    {PPUDATA}('P')
    {PPUDATA}('S')
    ppu_reset_addr($3F03)
    {PPUDATA}(main_hz_color)

fn main_hz_update_state_start()
    tap_count = 0
    start_index = (frame_index << 4) + UU(subframe_index)
    end_index = start_index

fn main_hz_update_state_poll(U i, Bool poll)
    if poll && !prev_poll_on
        if tap_count < $FF
            tap_count += 1
        end_index = (frame_index << 4) + UU(i) - 16

fn main_hz_update_state_record()
    // big computation
    if tap_count > 1 && tap_count < $FF
        // should always fit
        UUU hz_dividend = (NTSC_FRAMERATE * (tap_count - 1)) << 3
        UU interval = end_index - start_index
        UU hz = UU(udiv_24(hz_dividend, interval) << 1)
        hz_digits = U[5](uu_to_ddddd(hz))
        // branch on high byte of hz to set color
        main_hz_color = get_hz_color(hz.b)

fn main_hz_update_bg()
    ppu_reset_addr($2063)
    if hz_digits[4] != 0
        {PPUDATA}($F0+hz_digits[4])
    else
        {PPUDATA}('·')
    {PPUDATA}($F0+hz_digits[3])
    {PPUDATA}($F0+hz_digits[2])
    {PPUDATA}('.')
    {PPUDATA}($F0+hz_digits[1])
    {PPUDATA}($F0+hz_digits[0])
    ppu_reset_addr($20A4)
    {PPUDATA}(max(tap_count, 1))
    ppu_reset_addr($3F03)
    {PPUDATA}(main_hz_color)

fn get_hz_color(U i) U
    if i < $0B
        return HZ_GRADIENT[i]
    for U j = 0; j < 7; j += 1
        if i < HZ_THRESHOLDS[j]
            return HZ_GRADIENT[j + 11]
    return HZ_GRADIENT[18]
