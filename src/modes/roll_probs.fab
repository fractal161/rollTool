/*
 * ROLL PROBS
 */

// packed_info consists of "length" (first 5 bits) and "tap count" (last 3 bits)
// $F0 is used to represent an uninitialized value, since a 1-tap must have
// a length of 0.
// hz can be calculated using 60*length / taps-1
struct TapInfo
    U packed_info
    U start_index

struct TapFreq
    U packed_info
    U freq

vars /roll_probs
: +zero_page
    TapInfo[16] sub_info
    TapFreq[16] freqs
    UU sub_prev_poll_on

data /roll_probs
    [] rp_title
        ("ROLL·PROBS")

// updates the background for the current viz. assumes that it's already empty
fn roll_probs_init_bg()
    ppu_reset_addr($2050)
    for U i = 0; i < len(rp_title); i += 1
        {PPUDATA}(@rp_title[i])
    UU row_addr = $208E
    for U i = 0; i < 3; i += 1
        ppu_reset_addr(row_addr)
        {PPUDATA}('%')
        {PPUDATA}('·')
        {PPUDATA}('·')
        {PPUDATA}('T')
        {PPUDATA}('·')
        {PPUDATA}('·')
        {PPUDATA}('*')
        {PPUDATA}('·')
        {PPUDATA}('·')
        {PPUDATA}('·')
        {PPUDATA}('%')
        {PPUDATA}('·')
        {PPUDATA}('·')
        {PPUDATA}('T')
        {PPUDATA}('·')
        {PPUDATA}('·')
        {PPUDATA}('*')
        row_addr += $40

fn roll_probs_update_state_start()
    for U i = 0; i < 16; i += 1
        sub_info[i].packed_info = $F0
        freqs[i].freq = $00

fn roll_probs_update_state_poll(U i, Bool poll)
    // to update sub_prev_poll_on, shift in poll and shift out prev_poll
    Bool tmp_prev_poll_on = sub_prev_poll_on & UU(1)
    poll >=> sub_prev_poll_on
    if poll && !tmp_prev_poll_on
        U taps = sub_info[i].packed_info & %111
        // first input for subframe slice
        if sub_info[i].packed_info == $F0
            // length of 0 because first input, and 1 tap
            sub_info[i].packed_info = $00
            sub_info[i].start_index = frame_index.a
        // means there are less than 8 inputs
        else if taps != %111 && frame_index.a < 31
            U tap_len = frame_index.a - sub_info[i].start_index
            sub_info[i].packed_info = tap_len << 3 | (taps + 1)

// TODO TODO TODO: actually handle empty slices correctly

fn roll_probs_update_state_record()
    // clear freq list by setting all frequencies to 0
    for U i = 0; i < 16; i += 1
        freqs[i].freq = $00
    // take all measurements (taps+duration) and put them in frequency list
    for U i = 0; i < 16; i += 1
        U packed = sub_info[i].packed_info
        for U j = 0; j < 16; j += 1
            if freqs[j].freq == 0
                freqs[j].packed_info = packed
                freqs[j].freq = 1
                break
            if packed == freqs[j].packed_info
                freqs[j].freq += 1
                break

fn roll_probs_update_sprites(U oam_index) U
    UU taken_freqs = $00
    // iterate through freq list, taking the max freq each time
    for U i = 0; i < 3; i += 1
        UU freq_flag = 1
        U max_freq = 0
        U max_index = 0
        UU max_flag = 0
        for U j = 0; j < 16; j += 1
            // means end of list has been reached
            if freqs[j].freq == 0
                break
            // make sure entry hasn't already been considered
            if freq_flag & taken_freqs > 0
                freq_flag <<= 1
                continue
            if freqs[j].freq > max_freq
                max_freq = freqs[j].freq
                max_index = j // code for this is just not present????
                max_flag = freq_flag
            freq_flag <<= 1
        // means that everything has been taken
        if max_freq == 0
            return oam_index
        taken_freqs |= max_flag
        U length = freqs[max_index].packed_info >> 3
        // should be +1, but this makes formula a bit quicker
        U taps = freqs[max_index].packed_info & %111
        // compute hz digits (this time for 60hz and not 960hz)
        UU hz_dividend = U(60) * taps
        UU hz = udiv_16(hz_dividend, length)
        U[3] prob_hz_digits = U[3](uu_to_ddd(hz)) // hz_digits[4] is always 0
        if taps == 0
            prob_hz_digits[1] = 0
            prob_hz_digits[0] = 0
        // write probs
        U height = 31 + (i << 4)
        set_oam(oam_index, 104, height, max_freq, %11)
        oam_index += 4
        // write tap counts
        if freqs[max_index].packed_info == $F0
            set_oam(oam_index, 128, height, 0, %11)
        else
            set_oam(oam_index, 128, height, taps+1, %11)
        oam_index += 4
        // write hz digits
        set_oam(oam_index, 144, height, prob_hz_digits[1], %11)
        oam_index += 4
        set_oam(oam_index, 152, height, prob_hz_digits[0], %11)
        oam_index += 4

    return oam_index

fn roll_probs_update_bg()

// restores the background to empty
fn roll_probs_clean_bg()
    ppu_reset_addr($2050)
    for U i = 0; i < len(rp_title); i += 1
        {PPUDATA}('·')
    UU row_addr = $208E
    for U i = 0; i < 3; i += 1
        ppu_reset_addr(row_addr)
        for U j = 0; j < 17; j += 1
            {PPUDATA}('·')
        row_addr += $40
